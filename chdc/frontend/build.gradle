
plugins {
    id "java"
}

ext {
    themeOutputDir = "$buildDir/theme"
    outputDir = "$buildDir/gwt-out"
}


dependencies {

    compile "com.google.guava:guava:21.0"

    compile libraries.immutables.annotations

    compile "com.google.gwt:gwt-user:2.8.0"
    compile 'com.google.guava:guava-gwt:21.0'

    compile libraries.gwt.gson
    compile libraries.rebar.appcache

    compile group: 'com.sencha.gxt', name: 'gxt', version: '4.0.0'
    compile group: 'com.sencha.gxt', name: 'gxt-chart', version: '4.0.0'
    compile group: 'com.sencha.gxt', name: 'gxt-theme-triton', version: '4.0.0'

    compile "com.google.gwt:gwt-dev:2.8.0"
    compile "com.sencha.gxt:gxt-themebuilder:4.0.0"

    compile project(':ui:app')
}

sourceSets {
    theme {
        java.srcDir themeOutputDir
    }
}


task generateTheme(type: JavaExec) {

    def themeFile = project.file('src/main/theme/chdc.groovy')

    inputs.file themeFile

    classpath configurations.compile

    main = 'com.sencha.gxt.themebuilder.ThemeBuilder'

    args '-gen', themeOutputDir
    args '-slicedDisabled', 'true'
    args '-sourcesOnly', 'true'

    args themeFile

}

compileJava {
    dependsOn generateTheme
    source    += sourceSets.theme.java
}

task compileApp(type: JavaExec) {

    outputs.dir outputDir
    inputs.dir sourceSets.main.allSource.srcDirs

    // Compiled java classes and libraries second
    classpath "$buildDir/theme"
    classpath sourceSets.main.java.srcDirs
    classpath sourceSets.main.resources.srcDirs
    classpath sourceSets.main.runtimeClasspath

    main = 'com.google.gwt.dev.Compiler'

    args '-logLevel', 'INFO'

    // Disable class metadata -- Class.getName() will not
    // be available to compiled code, but we save ~150k
    args '-XnoclassMetadata'

    args '-war', outputDir

    args '-extra', "${buildDir}/gwt-extra"

    args '-saveSource'
    args '-gen', "${buildDir}/gwt-gen"

    if("true".equals(project.property("gwtSoyc"))) {
        args '-compileReport'
    }
    args 'chdc.frontend.Chdc'
}

compileApp.onlyIf { !project.hasProperty('skipGwt') }

